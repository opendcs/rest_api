/*
 *  Copyright 2025 OpenDCS Consortium and its Contributors
 *
 *  Licensed under the Apache License, Version 2.0 (the "License")
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

plugins {
    id "opendcs-rest-api.java-conventions"
    id "opendcs-rest-api.deps-conventions"
    id "opendcs-rest-api.publishing-conventions"
    id "war"
}

dependencies {
    implementation(libs.servlet.api)
    implementation(libs.slf4j.api)
    runtimeClasspath(libs.jstl)
    runtimeClasspath(libs.slf4j.jdk14)

    runtimeOnly("org.webjars:bootstrap:5.3.8")
    runtimeOnly("org.webjars:jquery-blockui:2.70-1")
    runtimeOnly("org.webjars:jquery:3.4.1")
    runtimeOnly("org.webjars:datatables:2.3.2")
    runtimeOnly("org.webjars:datatables-buttons:3.2.5")
    runtimeOnly("org.webjars:datatables-select:2.1.0")
    runtimeOnly("org.webjars:select2:4.0.13")
    runtimeOnly("org.webjars:d3js:6.7.0")
    runtimeOnly("org.webjars:datatables-responsive:2.2.7")
    runtimeOnly("org.webjars:bootstrap-switch:3.3.4")
    runtimeOnly("org.webjars:uniform:2.1.2-1")
    runtimeOnly("org.webjars.npm:switchery:0.0.2") {
        exclude group: "org.webjars.npm", module: "cssify"
    }
    runtimeOnly("org.webjars:datatables-rowreorder:1.4.1")



    runtimeOnly("org.webjars:webjars-locator-core:0.59")
    runtimeOnly("org.webjars:webjars-servlet-2.x:1.6")
    

    /*
    runtimeOnly("org.webjars:ladda-bootstrap:0.1.0")
    runtimeOnly("org.webjars:spin-js:2.1.0")
    
    
    runtimeOnly("org.webjars:jquery-ui:1.12.1")
    runtimeOnly("org.webjars:alpaca:1.5.24")
    runtimeOnly("org.webjars:typeaheadjs:0.11.1")
    runtimeOnly("org.webjars:handlebars:4.0.5")
    runtimeOnly("org.webjars:echarts:5.5.0")
    */

    testImplementation(libs.selenium)
    testImplementation(libs.junit.api)
    testImplementation(libs.mockito.core)
    testImplementation(libs.mockito.junit.jupiter)
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = war.archiveBaseName.get()
            from components.java
        }
    }
}

if (JavaVersion.current() != JavaVersion.VERSION_1_8) {
    sonarqube {
        properties {
            property 'sonar.sources', 'src/main/java,src/main/webapp'
            property 'sonar.exclusions', 'src/main/webapp/resources/css/icons/**'
        }
    }
}

tasks.register('integrationTest', Test) {
    group "verification"
    useJUnitPlatform {
        includeTags "integration"
    }
    shouldRunAfter test
    dependsOn war
    systemProperties += project.properties.findAll { k, v -> k.startsWith("opendcs") }
}

test {
    useJUnitPlatform {
        excludeTags "integration"
    }
}