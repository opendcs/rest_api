/*
 *  Copyright 2025 OpenDCS Consortium and its Contributors
 *
 *  Licensed under the Apache License, Version 2.0 (the "License")
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

plugins {
    id "opendcs-rest-api.java-conventions"
    id "opendcs-rest-api.deps-conventions"
    id "opendcs-rest-api.publishing-conventions"
    id "war"
}

dependencies {
    implementation(libs.servlet.api)
    implementation(libs.slf4j.api)
    implementation(libs.jstl)
    implementation(libs.slf4j.jdk14)

    // Webjars
    implementation("org.webjars:webjars-locator-core:0.59")
    implementation("org.webjars:webjars-servlet-2.x:1.6")
    implementation("org.webjars:jquery:3.7.1")
    implementation("org.webjars:bootstrap:5.3.8")
    implementation("org.webjars:momentjs:2.30.1-1")
    implementation("org.webjars.npm:bootstrap-daterangepicker:3.1.0")
    implementation("org.webjars:jquery-blockui:2.70-1")
    implementation("org.webjars:jquery-validation:1.20.0")
    implementation("org.webjars:d3js:6.7.0")

    // DataTables + Bootstrap 5
    implementation("org.webjars.npm:datatables.net:1.13.8")
    implementation("org.webjars.npm:datatables.net-bs5:1.13.8")
    implementation("org.webjars.npm:datatables.net-responsive:2.5.0")
    implementation("org.webjars.npm:datatables.net-responsive-bs5:2.5.0")
    implementation("org.webjars.npm:datatables.net-buttons:2.4.2")
    implementation("org.webjars.npm:datatables.net-buttons-bs5:2.4.2")
    implementation("org.webjars.npm:datatables.net-select:1.7.1")
    implementation("org.webjars.npm:datatables.net-select-bs5:1.4.0")

    // Extras
    implementation("org.webjars:select2:4.0.13")
    implementation("org.webjars:bootstrap-switch:3.3.4")
    implementation("org.webjars:uniform:2.1.2-1")
    implementation("org.webjars:bootstrap-sidebar:0.2.2")
    implementation("org.webjars:pickadate.js:3.6.4")

    testImplementation(libs.selenium)
    testImplementation(libs.junit.api)
    testImplementation(libs.mockito.core)
    testImplementation(libs.mockito.junit.jupiter)
}


publishing {
    publications {
        maven(MavenPublication) {
            artifactId = war.archiveBaseName.get()
            from components.java
        }
    }
}

if (JavaVersion.current() != JavaVersion.VERSION_1_8) {
    sonarqube {
        properties {
            property 'sonar.sources', 'src/main/java,src/main/webapp'
            property 'sonar.exclusions', 'src/main/webapp/resources/css/icons/**'
        }
    }
}

tasks.register('integrationTest', Test) {
    group "verification"
    useJUnitPlatform {
        includeTags "integration"
    }
    shouldRunAfter test
    dependsOn war
    systemProperties += project.properties.findAll { k, v -> k.startsWith("opendcs") }
}

test {
    useJUnitPlatform {
        excludeTags "integration"
    }
}