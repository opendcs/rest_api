/*
 *  Copyright 2024 OpenDCS Consortium and its Contributors
 *
 *  Licensed under the Apache License, Version 2.0 (the "License")
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

plugins {
    id "opendcs-rest-api.java-conventions"
    id "opendcs-rest-api.deps-conventions"
    id "opendcs-rest-api.publishing-conventions"
    id "war"
}

configurations {
    webjars
}

dependencies {
    api(libs.opendcs) {
        //Workaround for the excessive opendcs dependencies
        exclude group: '*', module: '*'
    }
    implementation(libs.servlet.api)
    implementation(libs.json.jackson)
    implementation(libs.jersey.container.servlet)
    implementation(libs.nimbus)
    implementation(libs.jwt)
    implementation(libs.jersey.hk2)
    implementation(libs.slf4j.jdk)
    runtimeOnly(libs.jaxb.runtime)
    runtimeOnly(libs.postgresql)
    webjars(libs.swagger.ui)
    testImplementation(libs.jersey.test)
    testImplementation(libs.mockito.core)
    testImplementation(libs.mockito.inline)
    testRuntimeOnly(libs.byte.buddy)
    testImplementation(libs.mockito.junit.jupiter)
    testImplementation(libs.rest.assured)
    testImplementation(libs.hamcrest)
    testImplementation(libs.bundles.tomcat)
    compileOnly(libs.auto.service)
    annotationProcessor(libs.auto.service)
}

task extractWebJars(type: Copy) {
    from zipTree(configurations.webjars.singleFile)

    into file("${project.layout.getBuildDirectory().get().getAsFile().toString()}/resources/main/swaggerui")
    includeEmptyDirs false
    eachFile {
        path -= ~/^.+?\/.+?\/.+?\/.+?\/.+?\//
    }
    exclude "**/swagger-initializer.js"
    includeEmptyDirs = false
}

compileTestJava {
    dependsOn extractWebJars
}

javadoc {
    dependsOn extractWebJars
}

jar {
    dependsOn extractWebJars
}

war {
    dependsOn extractWebJars
    from 'src/main/webapp'
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = war.archiveBaseName.get()
            from components.java
        }
    }
}

if (JavaVersion.current() != JavaVersion.VERSION_1_8) {
    sonarqube {
        properties {
            property 'sonar.exclusions', 'src/main/resources/swaggerui/**'
        }
    }
}


tasks.register('generateConfig', Copy) {
    doFirst {
        project.delete "$buildDir/tomcat/"
        project.mkdir "$buildDir/tomcat/logs"
    }
    from 'src/test/resources/tomcat'
    into "$buildDir/tomcat"

    //rename "context.xml", "context.xml.template"
    rename "tomcat-server.xml", "server.xml"

    inputs.dir "src/test/resources"
    outputs.dir "$buildDir/tomcat/conf"
}

tasks.register('integrationTest', Test) {
    group "verification"
    useJUnitPlatform {
        includeTags "integration"
    }
    shouldRunAfter test
    dependsOn war
    dependsOn generateConfig
    jvmArgs += "-DwarContext=/opendcs-rest-api"
    jvmArgs += "-Djava.util.logging.config.file=$projectDir/test/resources/logging.properties"
    jvmArgs += "-Dorg.apache.tomcat.util.digester.PROPERTY_SOURCE=org.apache.tomcat.util.digester.EnvironmentPropertySource"
    jvmArgs += "-Dcatalina.base=$buildDir/tomcat"
    systemProperties += project.properties.findAll { k, v -> k.startsWith("opendcs") }
    systemProperties += project.properties.findAll { k, v -> k.startsWith("postgresdb") }
    if (!project.hasProperty("postgresdb.url")) {
        systemProperties.put("postgresdb.url", "jdbc:postgresql://localhost:5432/open_tsdb")
        systemProperties.put("opendcs.db.username", "tsdbadm")
        systemProperties.put("opendcs.db.password", "postgres_pass")
    }
    dependsOn(":opendcs-rest-api-jetty:startPostgresContainer")
    finalizedBy(":opendcs-rest-api-jetty:stopPostgresContainer")
}

test {
    useJUnitPlatform {
        excludeTags "integration"
    }
}
