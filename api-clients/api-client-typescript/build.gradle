plugins {
    id 'base'
    id 'opendcs-rest-api.deps-conventions'
    id "org.openapi.generator" version "7.17.0"
    id 'com.github.node-gradle.node' version '7.1.0'
}

configurations {
    apiSpec
}

dependencies {
    apiSpec(project(path: ":opendcs-rest-api", configuration: 'api_spec'))
}

openApiGenerate {
    inputSpec = configurations.apiSpec.asPath
    outputDir = "$buildDir/generated/openApi"
    generatorName = "typescript"
    // packageName = "org.opendcs"
    // apiPackage = "org.opendcs"
    // modelPackage = "org.opendcs"
    // invokerPackage = "org.opendcs"
    globalProperties.set([
        modelDocs: "true",
        modelTests: "false",
        apiDocs: "true",
        apiTests: "false",
        enabledPostProcessFile: "false"
    ])
    ignoreFileOverride = "$projectDir/.openapi-generator-ignore"
}

openApiValidate {
    inputSpec = configurations.apiSpec.asPath
}


def genTask = tasks.named("openApiGenerate")

genTask.configure {
    dependsOn configurations.apiSpec
    mustRunAfter npmInstall
}

node {
    // Version of Node.js to install. Required.
    version = '20.10.0'
    
   
    // Whether to download Node.js if it is not already installed. Defaults to true.
    download = true
    
    // The working directory for Node.js. Defaults to the project directory.
    nodeProjectDir = project.layout.buildDirectory.dir("generated/openApi")
    
    // Configure a proxy for the download. Optional.
    // distBaseUrl = 'https://custom.node.dist.mirror'
}

task bundle(type: NpxTask) {
    dependsOn genTask
    dependsOn npmInstall
    command = "npm"
    args = ['run', 'build']
}

build.dependsOn bundle